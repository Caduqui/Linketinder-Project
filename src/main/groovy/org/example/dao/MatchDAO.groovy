package org.example.dao

import org.example.ConexaoBanco
import org.example.Match
import org.example.repositorio.CandidatoRepositorio
import org.example.repositorio.EmpresaRepositorio
import org.example.repositorio.MatchRepositorio
import org.example.repositorio.VagaRepositorio

import java.sql.PreparedStatement
import java.sql.ResultSet

/**
 *
 * @author Guilherme Lima Conte
 */

class MatchDAO implements MatchRepositorio{

    final ConexaoBanco conexaoBanco
    final CandidatoRepositorio candidatoRepositorio
    final EmpresaRepositorio empresaRepositorio
    final VagaRepositorio vagaRepositorio

    MatchDAO(ConexaoBanco conexaoBanco, CandidatoRepositorio candidatoRepositorio, EmpresaRepositorio empresaRepositorio, VagaRepositorio vagaRepositorio) {
        this.conexaoBanco = conexaoBanco
        this.candidatoRepositorio = candidatoRepositorio
        this.empresaRepositorio = empresaRepositorio
        this.vagaRepositorio = vagaRepositorio
    }

    @Override
    boolean criarMatchSePossivel(Integer idCandidato, Integer idEmpresa, Integer idVaga) {
        String sql = """
            INSERT INTO matches (id_candidato,id_empresa,id_vaga)
                SELECT ccv.id_candidato, cec.id_empresa, ccv.id_vaga
                FROM curtida_candidato_vaga AS ccv, curtida_empresa_candidato AS cec, vagas AS v
                WHERE ccv.id_candidato = ?
                AND cec.id_empresa = ?
                AND ccv.id_vaga = ?
                AND ccv.id_candidato = cec.id_candidato
                AND ccv.id_vaga = cec.id_vaga
                AND v.id = ccv.id_vaga
                AND v.id_empresa = cec.id_empresa
                ON CONFLICT (id_candidato,id_empresa,id_vaga) DO NOTHING
        """

        conexaoBanco.executar(sql) { PreparedStatement statement ->
            statement.setInt(1, idCandidato)
            statement.setInt(2, idEmpresa)
            statement.setInt(3, idVaga)
            statement.executeUpdate() > 0
        }
    }

    @Override
    List<Match> listar() {
        String sql = """
            SELECT * FROM matches ORDER BY id
        """

        conexaoBanco.executar(sql) { PreparedStatement statement ->
            statement.executeQuery().withCloseable { ResultSet resultado ->
                List<Match> matches = []
                while (resultado.next()) {
                    matches << mapearMatch(resultado)
                }
                return matches
            }
        }
    }

    Match mapearMatch(ResultSet resultado) {
        return new Match(
                candidatoRepositorio.buscarPorId(resultado.getInt("id_candidato")),
                empresaRepositorio.buscarPorId(resultado.getInt("id_empresa")),
                vagaRepositorio.buscarPorId(resultado.getInt("id_vaga"))
        )
    }
}
